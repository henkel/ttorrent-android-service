apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.2'

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 21
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
        }
    }
    sourceSets {
        main {
        	manifest.srcFile 'AndroidManifest.xml'
        }
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile 'com.turn:ttorrent-core:1.5'
    compile 'com.android.support:support-v4:23.2.0'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.9.5'
    testCompile 'org.robolectric:robolectric:3.0'
    testCompile 'org.robolectric:shadows-support-v4:3.0'
    testCompile 'org.assertj:assertj-core:1.7.0'
    testCompile 'org.slf4j:slf4j-nop:1.7.13'
}


def siteUrl = 'https://github.com/henkel/ttorrent-android-service'
def gitUrl = 'https://github.com/henkel/ttorrent-android-service.git'
def issueUrl = 'https://github.com/henkel/ttorrent-android-service/issues'

group = 'de.sulaco.bittorrent'
version = '0.2.2'

project.archivesBaseName = 'ttorrent-android-service'

Properties properties = new Properties()
def localProperties = project.rootProject.file('local.properties')
if (localProperties.exists()) {
    properties.load(localProperties.newDataInputStream())
}

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives'] //When uploading configuration files
    pkg {
        repo = 'maven'
        name = 'ttorrent-android-service'
        desc = 'Android BitTorrent IntentService based on the ttorrent library.'
        websiteUrl = siteUrl
        issueTrackerUrl = issueUrl
        vcsUrl = gitUrl
        licenses = ['Apache-2.0']
        labels = ['bittorrent', 'ttorrent', 'android', 'aar']
        publicDownloadNumbers = true
        githubRepo = 'henkel/ttorrent-android-service'
        githubReleaseNotesFile = 'README.md'
    }
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name 'Android BitTorrent Service'
                url siteUrl
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'henkel'
                        name 'Philipp Henkel'
                        email 'weltraumpilot@googlemail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

task findConventions << {
    println project.getConvention()
}